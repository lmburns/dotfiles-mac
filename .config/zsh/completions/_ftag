#compdef ftag

setopt localoptions warncreateglobal typesetsilent

typeset -a local_tags
_tags_local() {
  local_tags=( "${(fu)$(tag -Nl)}" )
}

typeset -a global_tags
_tags_global() {
  global_tags=( "${(fu)$(tag -tNf '*')}" )
}

autoload -U is-at-least
typeset -a _arguments_options
local ret=1

if is-at-least 5.2; then
    _arguments_options=(-s -S -C)
else
    _arguments_options=(-s -C)
fi

local context curcontext="$curcontext" state line arguments

arguments=(
    {-w,--wutag}'[Also tag files with wutag]'
    {-q,--query}'[Start fzf with a query]:--Query--:->query'
    {-h,--help}'[Display a help message]'
    {-b,--boxes}'[Add a box aroud header with boxes]'
    {-c,--lolcat}'[Color to header with lolcat]'
    {-t,--toilet}'[Color and format header with toilet]'
    {-l,--local}'[Use tags in local directory]:local:->local_tags'
    {-z,--zoxide}'[Start zoxide to select directory to use tags]'
    {-j,--jump}'[Start autojump to select directory to use tags]'
  )

_arguments "${_arguments_options[@]}" $arguments \
          && ret=0
          # '*: :->args' \

case $state in
  query)
    # Specify all tags explicitly for query
    _tags_local
    _tags_global
    _alternative 'gtags:-- Global Tags --:compadd -a - global_tags' \
      'ltags:-- Local Tags --:compadd -a - local_tags' && \
      ret=0
    ;;
  local_tags)
    # List only local tags for '-l'
    _tags_local
    _describe -t local_tags '-- Local Tags --' local_tags && ret=0
    ;;
  *)
    # Global search is default
    _tags_local
    _tags_global
    _alternative 'gtags:-- Global Tags --:compadd -a - global_tags' \
      'ltags:-- Local Tags --:compadd -a - local_tags' && \
      ret=0
    ;;
esac
